apply plugin: 'com.android.application'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.example.android.architecture.blueprints.todomvp"
        minSdkVersion 19
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        flavorDimensions "debug"
    }

    buildTypes {
        debug {
            minifyEnabled true
            // Uses new built-in shrinker http://tools.android.com/tech-docs/new-build-system/built-in-shrinker
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }

        release {
            minifyEnabled true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
    }

    // If you need to add more flavors, consider using flavor dimensions.
    productFlavors {
        mock {
            dimension "debug"
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension "debug"
        }
    }

    // Remove mockRelease as it's not needed.
    android.variantFilter { variant ->
        if(variant.buildType.name.equals('release')
                && variant.getFlavors().get(0).name.equals('mock')) {
            variant.setIgnore(true);
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

/*
 Dependency versions are defined in the top level build.gradle file. This helps keeping track of
 all versions in a single place. This improves readability and helps managing project complexity.
 */
dependencies {
    // App's dependencies, including test
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "com.google.android.material:material:1.1.0-alpha01"
    implementation "androidx.recyclerview:recyclerview:1.0.0"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "androidx.test.espresso:espresso-idling-resource:3.1.1-alpha01"
    implementation "com.google.guava:guava:18.0"

    // Dependencies for local unit tests
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-all:1.10.19"
    testImplementation "org.hamcrest:hamcrest-all:1.3"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "androidx.test:runner:1.1.1-alpha01"
    androidTestImplementation "androidx.test:rules:1.1.1-alpha01"

    // Dependencies for Android unit tests
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation 'com.google.dexmaker:dexmaker:1.2'
    androidTestImplementation 'com.google.dexmaker:dexmaker-mockito:1.2'

    // Espresso UI Testing
    androidTestImplementation "androidx.test.espresso:espresso-core:3.1.1-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.1.1-alpha01"
    androidTestImplementation "androidx.test.espresso:espresso-intents:3.1.1-alpha01"

    // Resolve conflicts between main and test APK:
    androidTestImplementation "androidx.annotation:annotation:1.0.0"
    androidTestImplementation "androidx.legacy:legacy-support-v4:1.0.0"
    androidTestImplementation "androidx.recyclerview:recyclerview:1.0.0"
    androidTestImplementation "androidx.appcompat:appcompat:1.0.2"
    androidTestImplementation "com.google.android.material:material:1.1.0-alpha01"
}
